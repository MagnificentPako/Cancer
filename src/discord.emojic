📜 🔤json.emojic🔤
📜 🔤http.emojic🔤
📜 🔤events.emojic🔤

🌮 
    A class which let's you interact with Discord thingies.
🌮
🐇 ♋ 🍇
    🍰 socket 📞
    🍰 token 🔡
    🍰 headers 🍯🐚🔡
    🍰 http_client 🍑

    🌮
        The constructor for a discord instance
    🌮
    🐈 🔌 🍼token🔡 🍇
        🍮 socket 🚇 🔷📞🆕 🔤127.0.0.1🔤 6969
        🍮 headers 🔷🍯🐚🔡🐸

        🐷 headers 🔤User-Agent🔤 🔤DiscordBot (https://git.procrastination.life/Pako/Cancer, 1)🔤
        🐷 headers 🔤Authorization🔤 🍪 🔤Bot 🔤 token 🍪
        🐷 headers 🔤Content-Type🔤 🔤application/json🔤

        🍮 http_client 🔷🍑🆕 🔤localhost🔤 4242

        💬 socket 📇 🔤;🔤
        👂🐕
    🍉

    🌮 
        Receive one "frame"
    🌮
    🐖 👂 ➡️ 🔡 🍇
        🍮 finished 👎
        🍮 size_found 👎
        🍮 size_buffer 🔤🔤
        🔁 ❎size_found 🍇
            🍮 size_buffer 🍪 size_buffer 🍺🔡🍺👂 socket 1 🍪
            🍮 size_found ⛳size_buffer 🔤❌r❌n🔤
        🍉
        🍦 size 🍺🚂🔪size_buffer 0 ➖📐size_buffer 2 10
        🍎 🍺🔡🍺👂 socket size
    🍉

    🌮
        send one raw message
    🌮
    🐖 ✏ message 🔡 🍇
        💬 socket 📇 message
    🍉

    🌮
        shortcut for sending an IDENTIFY payload
    🌮
    🐖 👤 🍇
        🍦 d            🔷🍯🐚🚧🐸
        🍦 obj          🔷🍯🐚🚧🐸
        🍦 properties   🔷🍯🐚🚧🐸
                
        🐷 properties 🔤$os🔤       🔤linux🔤
        🐷 properties 🔤$browser🔤  🔤Cancer🔤
        🐷 properties 🔤$device🔤   🔤Cancer🔤

        🐷 d 🔤token🔤              token
        🐷 d 🔤compress🔤           👎
        🐷 d 🔤large_threshold🔤    250
        🐷 d 🔤properties🔤         properties

        🐷 obj 🔤op🔤 2
        🐷 obj 🔤d🔤  d          

        ✏🐕 🚧obj
    🍉

    🌮
        The bread and butter, or whatever you like to call it. Give this guy a callable and you will receive all the events you might ever want.
    🌮
    🐖☎ callback 🍇🔡🍉 🍇
        👤🐕
        🍦 start_time 🍩🕰💻
        🍦 current_s -1
        🍦 hello 👂🐕

        🔁👍 🍇
            🍦 raw 👂🐕
            🍭 callback raw
        🍉
    🍉

    🌮
        Send a message to a channel
    🌮
    🐖💬 channel🔡 message🔡 🍇
        🍦 body 🔷🍯🐚🚧🐸
        🐷 body 🔤content🔤 message

        😀message
        😀🚧body

        💦 http_client 🍪 🔤/api/v6/channels/🔤 channel 🔤/messages🔤 🍪 headers 🚧body
    🍉
🍉

🌮
    A class representing a User
🌮
🐇 👤 🍇
    🐊🚧

    🍰 id 🔡
    🍰 avatar 🔡
    🍰 username 🔡
    🍰 discriminator 🔡

    🐈🆕 raw🍯🐚⚪ 🍇
        🍮 id 🍺🔲🐽 raw 🔤id🔤 🔡
        🍮 avatar 🍺🔲🐽 raw 🔤avatar🔤 🔡
        🍮 username 🍺🔲🐽 raw 🔤username🔤 🔡
        🍮 discriminator 🍺🔲🐽 raw 🔤discriminator🔤 🔡
    🍉

    🐖🆔 ➡ 🔡 🍇 🍎id 🍉
    🐖📛 ➡ 🔡 🍇 🍎username 🍉

    🐖🚧 ➡ 🔡 🍇
        🍦 json 🔷🍯🐚🚧🐸
        🐷 json 🔤id🔤 id
        🐷 json 🔤avatar🔤 avatar 
        🐷 json 🔤username🔤 username
        🐷 json 🔤discriminator🔤 discriminator
        🍎 🚧json
    🍉
🍉

🌮
    A class representing a single Message
🌮
🐇 ✉ 🍇
    🐊🚧

    🍰 id 🔡
    🍰 author 👤
    🍰 content 🔡
    🍰 channel_id 🔡

    🐈🆕 raw 🍯🐚⚪ 🍇
        🍮 id 🍺🔲🐽 raw 🔤id🔤 🔡
        🍮 content 🍺🔲🐽 raw 🔤content🔤 🔡
        🍮 channel_id 🍺🔲🐽 raw 🔤channel_id🔤 🔡
        🍮 author 🔷👤🆕 🍺🔲🐽 raw 🔤author🔤 🍯🐚⚪ 
    🍉

    🐖🆔 ➡ 🔡 🍇 🍎id 🍉
    🐖👤 ➡ 👤 🍇 🍎author 🍉
    🐖💬 ➡ 🔡 🍇 🍎content 🍉
    🐖🏠 ➡ 🔡 🍇 🍎channel_id 🍉 

    🐖🚧 ➡ 🔡 🍇
        🍦 json 🔷🍯🐚🚧🐸
        🐷 json 🔤id🔤 id
        🐷 json 🔤author🔤 author
        🐷 json 🔤content🔤 content
        🐷 json 🔤channel_id🔤 channel_id
        🍎 🚧json
    🍉
🍉

