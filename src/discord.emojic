🌮 
    A class which let's you interact with Discord thingies.
🌮
🐇 🎅 🍇
    🍰 socket 📞
    🍰 token 🔡

    🌮 The constructor for a discord instance 🌮
    🐈 🔌 🍼token🔡 🍇
        🍮 socket 🚇 🔷📞🆕 🔤127.0.0.1🔤 6969
        💬 socket 📇 🔤;🔤
        👂🐕
    🍉

    🌮 Receive one "frame" 🌮
    🐖 👂 ➡️ 🔡 🍇
        🍮 finished 👎
        🍦 dat 🔷🍨🐚🔡🐸
        🔁 ❎finished 🍇
            🍦 char 🍺 🔡🍺👂 socket 1
            🍊 😛 char 🔤;🔤 🍇
                🍮 finished 👍
            🍉
            🍓 🍇
                🐻 dat char
            🍉
        🍉
        🍎 🔷🔡🍨 dat 🔤🔤
    🍉

    🌮 send one raw message 🌮
    🐖 ✏ message 🔡 🍇
        💬 socket 📇 message
    🍉

    🌮 shortcut for sending an IDENTIFY payload 🌮
    🐖 👤 🍇
        🍦 d            🔷🍯🐚🚧🐸
        🍦 obj          🔷🍯🐚🚧🐸
        🍦 properties   🔷🍯🐚🚧🐸
                
        🐷 properties 🔤$os🔤       🔤linux🔤
        🐷 properties 🔤$brwoser🔤  🔤Cancer🔤
        🐷 properties 🔤$device🔤   🔤Cancer🔤

        🐷 d 🔤token🔤              token
        🐷 d 🔤compress🔤           👎
        🐷 d 🔤large_threshold🔤    250
        🐷 d 🔤properties🔤         properties

        🐷 obj 🔤op🔤 2
        🐷 obj 🔤d🔤  d          

        ✏🐕 🚧obj
    🍉

    🌮 The bread and butter, or whatever you like to call it. Give this guy a callable and you will receive all the events you might ever want. 🌮
    🐖☎ callback 🍇🍯🐚⚪🍉 🍇
        👤🐕
        🍮 start_time 🍩🕰💻
        🍮 current_s -1
        🍦 hello 🍺🔲📰👂🐕 🚧

        🔁👍 🍇
            🍦 raw 👂🐕
            🍦 json 📰raw
            🍊🍦 parsed 🔲 json 🚧 🍇
                😀 🚧parsed
                👴🍭 callback parsed
            🍉🍓🍇
                😀 🍪🔤Failed to parse: 🔤 raw🍪
            🍉
        🍉
    🍉
🍉